" ------------------------------------------------------------
" Vim基本設定
" ------------------------------------------------------------
" リーダーキーの設定
let mapleader = ","

" ファイルタイプとインデントの設定
" ファイルタイプの検出、プラグインとインデントを有効化
filetype plugin indent on

" 検索設定
" 検索結果をハイライト表示
set hlsearch
" 検索時に大文字小文字を区別しない
set ignorecase
" 検索パターンに大文字を含む場合は大文字小文字を区別
set smartcase
" インクリメンタルサーチを有効化（検索文字入力時に随時検索）
set incsearch

" 矩形選択時の仮想編集を有効化
set virtualedit=block

" 折りたたみ設定
" インデントに基づいて折りたたみを行う
set foldmethod=indent
" デフォルトですべての折りたたみを開く
set foldlevel=99

" コマンドライン補完の設定
" 補完メニューを改善
set wildmenu wildoptions=pum
" ファイル名補完で大文字小文字を区別しない
set wildignorecase

" 行番号を相対的に表示
set relativenumber

" 一時ファイルの無効化
" スワップファイルを作成しない
set noswapfile
" バックアップファイルを作成しない
set nobackup
" アンドゥファイルを作成しない
set noundofile

" マウス操作を有効化
set mouse=a

" デフォルトシェルの設定
set shell=bash

" ヤンクをクリップボードに送信
augroup text_yank_post
  au!
  autocmd TextYankPost * :call system('clip -i', @")
augroup END

" ------------------------------------------------------------
" カーソル設定
" ------------------------------------------------------------
" Vimの起動時のカーソル設定
if has('vim_starting')
  " 挿入モード時: 非点滅の縦棒カーソル
  let &t_SI .= "\e[6 q"
  " ノーマルモード時: 非点滅のブロックカーソル
  let &t_EI .= "\e[2 q"
  " 置換モード時: 非点滅の下線カーソル
  let &t_SR .= "\e[4 q"
endif

" ------------------------------------------------------------
" インデント設定
" ------------------------------------------------------------
augroup fileTypeIndent
  autocmd!
  " Ruby: 2スペース（Rubyスタイルガイド推奨）
  autocmd FileType ruby setlocal expandtab shiftwidth=2 softtabstop=2

  " Go: タブ（公式ツール go fmt の標準）
  autocmd FileType go setlocal noexpandtab shiftwidth=4 softtabstop=4 tabstop=4

  " TypeScript/JavaScript: 2スペース（Google/Airbnb スタイルガイド準拠）
  autocmd FileType typescript setlocal expandtab shiftwidth=2 softtabstop=2
  autocmd FileType javascript setlocal expandtab shiftwidth=2 softtabstop=2
  autocmd FileType typescriptreact setlocal expandtab shiftwidth=2 softtabstop=2

  " Vim: 2スペース（一般的な慣習）
  autocmd FileType vim setlocal expandtab shiftwidth=2 softtabstop=2

  " YAML: 2スペース（YAML仕様の可読性推奨）
  autocmd FileType yaml setlocal expandtab shiftwidth=2 softtabstop=2
  autocmd FileType yml setlocal expandtab shiftwidth=2 softtabstop=2

  " JSON: 2スペース（一般的な慣習）
  autocmd FileType json setlocal expandtab shiftwidth=2 softtabstop=2

  " CSS: 2スペース（Google/Airbnb スタイルガイド準拠）
  autocmd FileType css setlocal expandtab shiftwidth=2 softtabstop=2
augroup END

" ------------------------------------------------------------
" パス設定
" ------------------------------------------------------------
" APIディレクトリのパス設定
set path+=api/.
set path+=api/app/**
set path+=api/bin/**
set path+=api/config/**
set path+=api/db/**
set path+=api/gen/**
set path+=api/lib/**
set path+=api/spec/**

" フロントエンドディレクトリのパス設定
set path+=frontend/.
set path+=frontend/src/**
set path+=frontend/tests/**

" 検索から除外するパターン
" ビルド・依存関係ディレクトリの除外
set wildignore+=**/node_modules/**
set wildignore+=**/tmp/**
set wildignore+=**/dist/**
set wildignore+=**/build/**
set wildignore+=**/.git/**

" 一時ファイル・バイナリファイルの除外
set wildignore+=*.log
set wildignore+=*.tmp
" 画像ファイルの除外
set wildignore+=*.png,*.jpg,*.gif
" ドキュメントファイルの除外
set wildignore+=*.pdf
" アーカイブファイルの除外
set wildignore+=*.zip,*.tar.gz

" ------------------------------------------------------------
" プラグイン設定
" ------------------------------------------------------------
" 互換性のないプラグインの読み込み
if has('syntax') && has('eval')
  packadd! matchit
end

" プラグインの定義
call plug#begin('~/.shared_cache/.vim/plugged')
  " 日本語ヘルプ
  Plug 'vim-jp/vimdoc-ja'
  " カラースキーム
  Plug 'morhetz/gruvbox'
  " ステータスライン
  Plug 'itchyny/lightline.vim'
    " Lightline用のGruvboxテーマ
    Plug 'shinchu/lightline-gruvbox.vim'
  " ファイルファインダー
  Plug 'ctrlpvim/ctrlp.vim'
    " あいまい検索機能の強化
    Plug 'mattn/ctrlp-matchfuzzy'
    " コマンドランチャー機能
    Plug 'mattn/ctrlp-launcher'
  " Git操作
  Plug 'tpope/vim-fugitive'
  " Git差分表示
  Plug 'airblade/vim-gitgutter'
  " コメントアウト
  Plug 'tyru/caw.vim'
  " LSPクライアント
  Plug 'prabirshrestha/vim-lsp'
  " LSP設定管理
  Plug 'mattn/vim-lsp-settings'
  " 非同期補完
  Plug 'prabirshrestha/asyncomplete.vim'
  " LSPによる補完
  Plug 'prabirshrestha/asyncomplete-lsp.vim'
  " コード実行
  Plug 'thinca/vim-quickrun'
  " Denops（Deno製プラグイン基盤）
  Plug 'vim-denops/denops.vim'
  " インデントガイド
  Plug 'nathanaelkane/vim-indent-guides'
call plug#end()

" 未インストールのプラグインを自動インストール
augroup auto_plug_install
  au!
  autocmd VimEnter * if len(filter(values(g:plugs), '!isdirectory(v:val.dir)'))
    \| PlugInstall --sync | source $MYVIMRC
  \| endif
augroup END

" ------------------------------------------------------------
" カラースキーム設定
" ------------------------------------------------------------
" ダークモードを使用
set background=dark
" gruvboxカラースキームを適用
colorscheme gruvbox

" ------------------------------------------------------------
" lightline.vim
" ------------------------------------------------------------
" 常にステータスラインを表示
set laststatus=2

" GUIでない場合は256色を有効化
if !has('gui_running')
  set t_Co=256
endif

" Lightlineの詳細設定
let g:lightline = {
  \ 'colorscheme': 'gruvbox',
  \ 'active': {
  \   'left': [ [ 'mode', 'paste' ],
  \             [ 'gitbranch', 'readonly', 'filename', 'modified' ],
  \           ],
  \   'right': [
  \     ['lineinfo'],
  \     ['percent'],
  \     ['fileformat', 'fileencoding', 'filetype', 'winbufnr']
  \   ]
  \ },
  \ 'inactive': {
  \   'right': [ [ 'lineinfo' ],
  \              [ 'percent' ],
  \              [ 'winbufnr' ] ]
  \ },
  \ 'component_function': {
  \   'gitbranch': 'fugitive#head',
  \ },
  \ 'component': {
  \   'winbufnr': 'win:%{winnr()} buf:%n'
  \ },
  \ }

" ------------------------------------------------------------
" vim-indent-guides
" ------------------------------------------------------------
" Vim起動時にインデントガイドを有効化
let g:indent_guides_enable_on_vim_startup = 1

" インデントガイドを表示し始めるレベル (2から開始)
let g:indent_guides_start_level = 2

" ガイドラインの幅を1文字分に設定
let g:indent_guides_guide_size = 1

" ------------------------------------------------------------
" ctrlp
" ------------------------------------------------------------
" あいまい検索機能の設定
let g:ctrlp_match_func = {'match': 'ctrlp_matchfuzzy#matcher'}
" 起動キーマッピング
let g:ctrlp_map = '<leader><leader>'
" Gitリポジトリ内のファイル一覧を取得するコマンド
let g:ctrlp_user_command = ['.git', 'cd %s && git ls-files -co --exclude-standard']

" バッファリストの表示
nnoremap <leader>b :CtrlPBuffer<cr>
" タグリストの表示
nnoremap <leader>t :CtrlPTag<cr>
" 現在のファイルのタグ一覧
nnoremap <leader>f :CtrlPBufTag<cr>
" Quickfixリストの表示
nnoremap <leader>q :CtrlPQuickfix<cr>
" 最近使用したファイルの一覧
nnoremap <leader>m :CtrlPMRUFiles<cr>

" ------------------------------------------------------------
" ctrlp-launcher
" ------------------------------------------------------------
" 起動キーマッピング
nnoremap <leader>e :<c-u>CtrlPLauncher<cr>
" ランチャー設定ファイルの場所を指定
let g:ctrlp_launcher_file = '~/.vim/config/.ctrlp-launcher'

" ------------------------------------------------------------
" ctags
" ------------------------------------------------------------
" タグファイルの検索パス（カレントディレクトリから親ディレクトリに向かって検索）
set tags=.tags;$HOME

" ctagsのオプションを共通定義
let s:ctags_options = '--exclude=node_modules --exclude=.git --exclude=*.css --exclude=*.json -R -f '

" タグファイル生成コマンド
command! MakeTags call system('ctags ' . s:ctags_options . '.tags')

" タグファイルの自動更新機能
function! s:execute_ctags() abort
  let tag_name = '.tags'
  let tags_path = findfile(tag_name, '.;')
  if tags_path ==# ''
    return
  endif

  " タグファイルのディレクトリパスを取得
  let tags_dirpath = fnamemodify(tags_path, ':p:h')
  " 見つかったタグファイルのディレクトリに移動して、ctagsをバックグラウンド実行（エラー出力破棄）
  execute 'silent !cd' tags_dirpath '&& ctags ' . s:ctags_options . tag_name . ' 2> /dev/null &'
endfunction

" ファイル保存時にタグファイルを自動更新
augroup update_ctags
  au!
  autocmd BufWritePost * call s:execute_ctags()
augroup END

" ------------------------------------------------------------
" vimdoc-ja
" ------------------------------------------------------------
" ヘルプの言語優先順位を設定（日本語を優先）
set helplang=ja,en

" ------------------------------------------------------------
" netrw
" ------------------------------------------------------------
" ツリー表示形式を標準に設定
let g:netrw_liststyle = 3
" バナーを非表示
let g:netrw_banner = 0
" プレビューウィンドウのサイズ
let g:netrw_winsize = 25

" Netrwの表示状態管理用フラグ
let g:NetrwIsOpen = 0

" Netrwをトグルで表示/非表示する関数
function! ToggleNetrw() abort
   if g:NetrwIsOpen
       " 開いている場合は閉じる
       let i = bufnr("$")
       while (i >= 1)
           if (getbufvar(i, "&filetype") == "netrw")
               silent exe "bwipeout " . i
           endif
           let i-=1
       endwhile
       let g:NetrwIsOpen = 0
   else
       " 閉じている場合は開く
       let g:NetrwIsOpen = 1
       silent Vex
   endif
endfunction

" ファイルパス取得関数
function! s:GetNetrwPath() abort
   " 現在のディレクトリツリーのパスを取得
   let l:dirpath = netrw#Call('NetrwTreePath', w:netrw_treetop)
   " カーソル位置のアイテム名を取得
   let l:filename = netrw#Call('NetrwGetWord')
   
   " ディレクトリの場合はディレクトリパスのみを返す
   " ファイルの場合はディレクトリパスとファイル名を結合
   return l:filename =~ '/$' ? l:dirpath : l:dirpath . '/' . l:filename
endfunction

" エンターキー押下時の処理
function! s:HandleEnter() abort
   let l:path = s:GetNetrwPath()

   if isdirectory(l:path)
       " ディレクトリの場合は標準のnetrw操作を実行
       execute "normal \<Plug>NetrwLocalBrowseCheck"
       return
   endif

   " ファイルの場合は指定ウィンドウで開く
   call s:OpenFileInSharedWindow('edit')
endfunction

" ファイルを開く処理
function! s:OpenFileInSharedWindow(mode) abort
   let l:path = s:GetNetrwPath()
   " 前回のウィンドウ番号を取得
   let l:prevwin = winnr('#')

   " ウィンドウが存在しない場合は新規作成
   if l:prevwin == 0
       vertical rightbelow new
       vertical resize 80
   else
       " 既存のウィンドウを使用
       execute l:prevwin . 'wincmd w'
   endif

   " ファイルを開く
   execute 'edit ' . fnameescape(l:path)

   " プレビューモードの場合はnetrwに戻る
   if a:mode ==# 'preview'
       wincmd p
   endif
endfunction

" キーマッピングの設定
function! s:set_netrw_mappings() abort
   if !exists('g:loaded_netrwPlugin')
       return
   endif
   " プレビュー表示用のマッピング
   nmap <buffer> p :call <SID>OpenFileInSharedWindow('preview')<CR>
   " エンターキーの動作設定
   nmap <buffer> <CR> :call <SID>HandleEnter()<CR>
endfunction

" Netrw初期設定
augroup InitNetrw
   autocmd!
   autocmd FileType netrw call s:set_netrw_mappings()
augroup END

" == でnetrwをトグル表示
noremap <silent>== :call ToggleNetrw()<CR>

" ------------------------------------------------------------
" gitgutter
" ------------------------------------------------------------
" gitgutter update faster
set updatetime=50

let g:gitgutter_sign_added = '+'
let g:gitgutter_sign_modified = '>'
let g:gitgutter_sign_removed = '-'
let g:gitgutter_sign_removed_first_line = '^'
let g:gitgutter_sign_modified_removed = '<'

" ref: https://teratail.com/questions/29844#reply-46767
augroup vimrc_vim_gitgutter
  au!
  autocmd VimEnter,ColorScheme * highlight SignColumn guibg=NONE ctermbg=NONE
  autocmd VimEnter,ColorScheme * highlight GitGutterAdd guibg=NONE ctermbg=NONE guifg=#000900 ctermfg=2
  autocmd VimEnter,ColorScheme * highlight GitGutterChange guibg=NONE ctermbg=NONE guifg=#bbbb00 ctermfg=3
  autocmd VimEnter,ColorScheme * highlight GitGutterDelete guibg=NONE ctermbg=NONE guifg=#ff2222 ctermfg=1
augroup END

" ------------------------------------------------------------
" asyncomplete
" ------------------------------------------------------------
" Added key mappings for asyncomplete
imap <expr> <Tab>   pumvisible() ? "\<C-n>" : "\<Tab>"
imap <expr> <S-Tab> pumvisible() ? "\<C-p>" : "\<S-Tab>"
imap <expr> <cr>    pumvisible() ? asyncomplete#close_popup() : "\<cr>"

let g:asyncomplete_popup_delay = 100

" ------------------------------------------------------------
" lsp
" ------------------------------------------------------------
let g:lsp_diagnostics_enabled = 1
let g:lsp_diagnostics_echo_cursor = 1
let g:lsp_diagnostics_echo_delay = 10
let g:lsp_diagnostics_float_cursor = 1
let g:lsp_diagnostics_signs_enabled = 1
let g:lsp_diagnostics_virtual_text_enabled = 0
let g:lsp_diagnostics_signs_delay = 10
let g:lsp_diagnostics_signs_insert_mode_enabled = 0
" let g:lsp_diagnostics_signs_error = {'text': '🐛'}
" let g:lsp_diagnostics_signs_warning = {'text': '🪰'}
" let g:lsp_diagnostics_signs_hint = {'text': '💡'}
" let g:lsp_diagnostics_signs_information = {'text': 'ℹ️'}
let g:lsp_diagnostics_highlights_delay = 10
let g:lsp_diagnostics_highlights_insert_mode_enabled = 0
let g:lsp_document_code_action_signs_enabled = 0

let g:lsp_settings_servers_dir='~/.shared_cache/.vim/servers'
let g:lsp_settings_filetype_ruby = ['solargraph']

" debug
" let g:lsp_log_verbose = 1 " ログを有効にする
" let g:lsp_log_file = expand('~/.shared_cache/.vim/vim-lsp.log') " ログの出力先

" vim-lsp がバッファで有効になったときに実行される関数
" バッファローカルなキーバインドやオプションを指定
" See: https://mattn.kaoriya.net/software/vim/20191231213507.htm
function! s:on_lsp_buffer_enabled() abort
  let g:lsp_format_sync_timeout = 1000

  " golang
  " バッファ保存時に毎回「import補完」と「コードフォーマット」を実行
  autocmd BufWritePre *.go call execute(['LspCodeActionSync source.organizeImports', 'LspDocumentFormatSync'])
endfunction

augroup lsp_install
  au!
  autocmd User lsp_buffer_enabled call s:on_lsp_buffer_enabled()
augroup END

" ------------------------------------------------------------
" session
" ------------------------------------------------------------
" augroup SessionAutocommands
"   au!
" 
"   autocmd VimEnter * nested call <SID>RestoreSessionWithConfirm()
"   autocmd VimLeave * execute 'SaveSession'
" augroup END

command! RestoreSession :source ~/.shared_cache/.vim/.session
command! SaveSession    :mksession! ~/.shared_cache/.vim/.session

" Restore session with confirm
" function! s:RestoreSessionWithConfirm()
"   let msg = 'Do you want to restore previous session?'
" 
"   if !argc() && confirm(msg, "&Yes\n&No", 1, 'Question') == 1
"     execute 'RestoreSession'
"   endif
" endfunction

" ------------------------------------------------------------
" QuickRun
" ------------------------------------------------------------
let g:quickrun_config = {}
let g:quickrun_config['go'] = {
    \ 'command': 'go',
    \ 'cmdopt': 'run',
    \ 'exec': 'go run %S'
    \ }

" ------------------------------------------------------------
" filetype
" ------------------------------------------------------------
augroup ts_react_add_path
  au!
  autocmd FileType typescriptreact :setl path+=**;/features/**
  autocmd FileType typescriptreact :setl includeexpr=substitute(v:fname,'^/','','')
augroup END

" ------------------------------------------------------------
" key mappings
" ------------------------------------------------------------
" highlight off
nmap <silent> <Esc><Esc> :<C-u>nohlsearch<CR><Esc>

" vimrcを開く
nmap <leader>v :e $MYVIMRC<CR>

" This line maps the 'q' key to a silent action
map q <silent>

" Git管理のファイルの相対パスをクリップボードにコピー
command! CopyGitPath :let @"='.' . substitute(expand('%:p'), trim(system('git rev-parse --show-toplevel')), '', '') | echo @" | call system('clip -i', @")
nnoremap <leader>c :CopyGitPath<cr>

" quickfix
nnoremap <silent> cn :cn<CR>
nnoremap <silent> cp :cp<CR>

" buffers and quickfix
function! ToggleQuickFix()
  if empty(filter(getwininfo(), 'v:val.quickfix'))
    copen
  else
    cclose
  endif
endfunction
nnoremap <leader>w :call ToggleQuickFix()<cr>
nnoremap <leader>d :bd<cr>

" searching ang grepping
nnoremap <leader>g :copen<cr>:Ggrep!<SPACE>
nnoremap K :Ggrep "\b<C-R><C-W>\b"<cr>:cw<cr>

" ctags
nnoremap <C-]> g<C-]>

function! GitShowCursorPullRequest()
  let l:word = expand('<cword>')
  let l:get_pr_number = "gh api graphql
      \ -F owner=':owner' -F repo=':repo' -F hash=" . l:word . "
      \ -f query='query($repo:String!, $owner:String!, $hash:String) { repository(name: $repo, owner: $owner) { object(expression: $hash) { ... on Commit {associatedPullRequests(first: 1) { edges { node { number } } } } } } }'
      \ | jq '.data.repository.object.associatedPullRequests.edges[0].node.number'"
  let l:pr_number = system(l:get_pr_number)

  echo "pr_number: " . l:pr_number
  if match(l:pr_number, 'null\n') != -1
    echo "Pull Request not found."
    return
  endif

  let l:get_pr_url = "gh api graphql
      \ -F owner=':owner' -F repo=':repo' -F number=" . str2nr(l:pr_number) . "
      \ -f query='query($repo:String!, $owner:String!, $number:Int!) { repository(name: $repo, owner: $owner) { pullRequest(number: $number) { url } } }'
      \ | jq '.data.repository.pullRequest.url'"
  let l:pr_url = system(l:get_pr_url)
  echo "open: " . l:pr_url | let @"=l:pr_url | call system('chrome -i', @")
endfunction

command! ShowPullRequest :call GitShowCursorPullRequest()
nnoremap go :ShowPullRequest<cr>

" 対応したspecファイルを開く
command! OpenSpec :call OpenSpecFile()

function! OpenSpecFile()
  " 現在のファイルパスを取得
  let current_file = expand('%:p')

  " app/ を spec/ に置換し、末尾に _spec を追加
  let spec_file = substitute(current_file, '/app/', '/spec/', '')
  let spec_file = substitute(spec_file, '\.rb$', '_spec.rb', '')

  " ファイルが存在するか確認
  if filereadable(spec_file)
    execute 'edit ' . spec_file
  else
    echo "Spec file not found: " . spec_file
  endif
endfunction

" 対応した実装ファイルを開く
command! OpenImplementation :call OpenImplementationFile()

function! OpenImplementationFile()
  let current_file = expand('%:p')
  let impl_file = substitute(current_file, '/spec/', '/app/', '')
  let impl_file = substitute(impl_file, '_spec\.rb$', '.rb', '')

  if filereadable(impl_file)
    execute 'edit ' . impl_file
  else
    echo "Implementation file not found: " . impl_file
  endif
endfunction
